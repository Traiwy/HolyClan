plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'ru.traiwy'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = 'https://jitpack.io' }
}

dependencies {

    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'mysql:mysql-connector-java:8.0.33'

    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

tasks {
    runServer {
        minecraftVersion("1.20.1")
    }

    shadowJar {
        archiveBaseName.set('HolyClans')
        archiveClassifier.set('')
        archiveVersion.set('')

        relocate 'com.zaxxer.hikari', 'ru.traiwy.holyclans.libs.hikari'
        relocate 'com.mysql', 'ru.traiwy.holyclans.libs.mysql'
    }

    build {
        dependsOn shadowJar
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}